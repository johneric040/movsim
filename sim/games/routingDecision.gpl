# gnuplot command file

# parameterised startStop command file
# invoke using -e option to set parameters, eg:
# gnuplot -e "lang='de'" routingDecisions.gpl

set datafile separator ","

# parameters
if (!exists("inputPath")) inputPath = "./"
if (!exists("outputPath")) outputPath = "./"
if (!exists("ldm")) ldm = ""
if (!exists("baseFile")) baseFile = "routing_test"
#print "LDM=".ldm

# variables
vehicle = "Equipped"
serviceprovider = "wiseGuy"

# input files
fundFile = inputPath.baseFile.".fund_".vehicle.".csv"
stFileA1 = inputPath.baseFile.".st.route_A1.csv"  ## spatiotemporal output
stFileA2 = inputPath.baseFile.".st.route_A2.csv"  ## spatiotemporal output
trajFileA1 = inputPath.baseFile.".traj.route_A1.csv"  
trajFileA2 = inputPath.baseFile.".traj.route_A2.csv"  
ttFileA1 = inputPath.baseFile.".tt.route_A1.csv"
ttFileA2 = inputPath.baseFile.".tt.route_A2.csv"
spFile = inputPath.baseFile.".serviceprov_".serviceprovider.".csv"
detFileA1(n) = sprintf(inputPath.baseFile.".det.road_3.x_%d.csv",n)
detFileA2(n) = sprintf(inputPath.baseFile.".det.road_10.x_%d.csv",n)

# output files
fundOutput = outputPath.baseFile.".fund_".vehicle.".eps"
traveltimeOutput = outputPath.baseFile.".traveltime.eps"
probabilityOutput = outputPath.baseFile.".probability.eps"
disutilityOutput= outputPath.baseFile.".disutility.eps"
disutilityErrorOutput= outputPath.baseFile.".disutility_error.eps"
v2dOutputA1 = outputPath.baseFile.".A1_v2d.eps"
v2dOutputA2 = outputPath.baseFile.".A2_v2d.eps"
trajOutputA1 = outputPath.baseFile.".A1_traj.eps"
trajOutputA2 = outputPath.baseFile.".A2_traj.eps"
detOutput = outputPath.baseFile.".det.eps"
detOutputTimeSeries = outputPath.baseFile.".det_timeseries.eps"

########  Languages  #####################################

fundTitle =  "Fundamental diagram Q({/Symbol r})"
timeMinuteLabel = "Time (min)"
distanceLabel = "Distance (m)"
velocityLabel = "Velocity (km/h)"
traveltimeLabel = "Inst. Link Traveltime (s)"
consumptionLabel = "Inst. Consumption (liters/s)"
cumTraveltimeLabel = "Cumulated Traveltime (h)"
cumConsumptionLabel = "Cumulated Consumption (liters)"
probLabel = "Probability (%)"
disutilityLabel  = "Disutility (s)"
disutilityErrorLabel  = "Disutility Error (s)"
densityLabel = "Density {/Symbol r} (vehicles/km)"
flowLabel = "Flow Q (vehicles/h)"

########  Line Styles  ###################################

set style line 1 linetype 1 linewidth 7 pointtype 7 pointsize 2 lc rgb "#000000"
set style line 2 linetype 7 linewidth 3 pointtype 5 pointsize 2 lc rgb "#dd0000"
set style line 3 linetype 1 linewidth 7 pointtype 9 pointsize 1.5 lc rgb "#ff8800"
set style line 4 linetype 3 linewidth 3 pointtype 11 pointsize 1.5 lc rgb "#00aa44"
set style line 5 linetype 5 linewidth 7 pointtype 13 pointsize 1.5 lc rgb "#220099"

set style line 1 linetype 11 linewidth 7 pointtype 7 pointsize 0.2 lc rgb "#000000"

min(x,y)=(x<y) ? x : y

set term post eps enhanced color dash "Helvetica" 26
#set nogrid
set grid


########################################################

set size 1,1
set title ""
set key box

########################################################


######### exact travel times  ###########################

set auto x
set auto y
set xlabel timeMinuteLabel
set ylabel traveltimeLabel
set out traveltimeOutput
plot\
 ttFileA1 using ($1/60.0):2 title "A1" with lines linestyle 1,\
 ttFileA2 using ($1/60.0):2 title "A2" with lines linestyle 3


######### disutilities  ################################

set auto x
set auto y
set xlabel timeMinuteLabel
set ylabel disutilityLabel
set out disutilityOutput
plot\
 spFile using ($1/60.):2 title "A1" with lines linestyle 1,\
 spFile using ($1/60.):5 title "A2" with lines linestyle 3
 
######### probability  ################################

set auto x
set auto y
set xlabel timeMinuteLabel
set ylabel probLabel
set out probabilityOutput
plot\
 spFile using ($1/60.):3 title "A1" with lines linestyle 1,\
 spFile using ($1/60.):6 title "A2" with lines linestyle 3

######### disutility errors  ################################

set auto x
set auto y
set xlabel timeMinuteLabel
set ylabel disutilityErrorLabel
set out disutilityErrorOutput
plot\
 spFile using ($1/60.):4 title "A1" with lines linestyle 1,\
 spFile using ($1/60.):7 title "A2" with lines linestyle 3


########  Plot Fundamental Diagrams  ###################

set xlabel "Density [1/km]"
set title ldm." ".fundTitle
set ylabel "Flow [1/h]"
set out fundOutput
plot fundFile using 1:4 title "" with lines linestyle 10

########  Detectors  ####################################
set xlabel densityLabel
set ylabel flowLabel
set out detOutput
plot\
 detFileA1(13000) using ($5/$4):5 title "13 km" with linespoints linestyle 1,\
 detFileA2(13000) using ($5/$4):5 title "13 km" with linespoints linestyle 2,\
 fundFile using 1:4 title "FD" with lines linestyle 10
 
set xlabel timeMinuteLabel
set ylabel flowLabel
set out detOutputTimeSeries
plot\
 detFileA1(13000) using ($1/60.):5 title "13 km" with linespoints linestyle 1,\
 detFileA2(13000) using ($1/60.):5 title "13 km" with linespoints linestyle 2


########  spatiotemporal output############################

set nokey
set notitle
set xlabel timeMinuteLabel
set ylabel "Position [km]"

set palette defined (0 "red", 20 "orange", 40 "yellow", 60 "green", 80 "blue",  100 "#dd00ff" )
set xrange [0:130]  
set cbrange [0:]

#set out v2dOutputA1
#plot stFileA1 using ($1/60.0):($2/1000.0):($3*3.6) with points ps 1 pt 7 palette
#set out v2dOutputA2
#plot stFileA2 using ($1/60.0):($2/1000.0):($3*3.6) with points ps 1 pt 7 palette

set out trajOutputA1
plot trajFileA1 using ($1/60.0):($3/1000.0):($4*3.6) with points ps 1 pt 7 palette

set out trajOutputA2
plot trajFileA2 using ($1/60.0):($3/1000.0):($4*3.6) with points ps 1 pt 7 palette

 
 
